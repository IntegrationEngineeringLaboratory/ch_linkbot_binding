; Script generated by the HM NIS Edit Script Wizard.
 
; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "Linkbot Labs C-STEM Edition"
!define LINKBOT_LABS_VERSION "0.7.11"
!define CHBAROBO_VERSION "1.0.4"
!define PRODUCT_PUBLISHER "Barobo"

!define LINKBOT_LABS_INSTALLER "Linkbot Labs-${LINKBOT_LABS_VERSION}-win32.exe"
!define CHBAROBO_DIR "chbarobo-${CHBAROBO_VERSION}-windows-multi"
!define PRODUCT_VERSION "${LINKBOT_LABS_VERSION} - ${CHBAROBO_VERSION}"
!define INSTALLER_NAME "${PRODUCT_NAME} ${PRODUCT_VERSION}.exe"
 
!finalize '"C:/Program Files/Microsoft SDKs/Windows/v7.0A/bin/signtool.exe" sign "${INSTALLER_NAME}"'

var chhome
var OUT
SetCompressor lzma
 
;!include "UserManagement.nsh"
 
 
; MUI 1.67 compatible ------
!include "MUI.nsh"
!include "NSISpcre.nsh"
!include "x64.nsh"
 
!insertmacro REReplace

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
 
; Welcome page
!insertmacro MUI_PAGE_WELCOME
; Components page
; !insertmacro MUI_PAGE_COMPONENTS
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!insertmacro MUI_PAGE_FINISH
 
; Language files
!insertmacro MUI_LANGUAGE "English"
 
; Reserve files
!insertmacro MUI_RESERVEFILE_INSTALLOPTIONS
 
; MUI end ------

;LangString DESC_python ${LANG_ENGLISH} "The Python Interpreter. This component is required to run Python programs."
;LangString DESC_pycharm ${LANG_ENGLISH} "The PyCharm IDE. This is a Python code editor with built-in execution and Python debugging tools."
 
Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "${INSTALLER_NAME}"
InstallDir "$PROGRAMFILES\LinkbotLabs C-STEM Edition"
ShowInstDetails show
 
Section -Main
    SetOutPath "$TEMP"
    SetOverwrite ifnewer
    File "${LINKBOT_LABS_INSTALLER}"
    ClearErrors
    ${If} ${Silent}
        ExecWait "$TEMP\${LINKBOT_LABS_INSTALLER} /S"
    ${Else}
        ExecWait "$TEMP\${LINKBOT_LABS_INSTALLER}"
    ${EndIf}

    ${If} ${Errors}
        ${Unless} ${Silent}
            MessageBox MB_OK|MB_ICONSTOP "Linkbot Labs installation failed, aborting."
        ${EndUnless}
        Abort
    ${EndIf}

    # Install the Ch package
    # First, figure out if Ch in installed already.
    ReadRegStr $chhome HKLM SOFTWARE\SoftIntegration "CHHOME"

    # Change slashes to backslashes
    ${REReplace} $OUT "\/" $chhome "\\" 1

    ${If} $OUT == ""
      StrCpy $OUT "C:\Ch"
      CreateDirectory "C:\Ch\toolkit\include"
    ${Else}
      RMDir /r "$OUT\package\chbarobo"
      Delete "$OUT\toolkit\include\mobot.h"
      Delete "$OUT\toolkit\include\linkbot.h"
    ${EndIf}
    SetOutPath "$OUT\package"
    File /r "${CHBAROBO_DIR}\chbarobo"

    ${If} ${RunningX64}
        Rename "$OUT\package\chbarobo\dl\win64\liblinkbot.dl" "$OUT\package\chbarobo\dl\liblinkbot.dl" 
        Rename "$OUT\package\chbarobo\bin\win64\msvcp120.dll" "$OUT\package\chbarobo\bin\msvcp120.dll" 
        Rename "$OUT\package\chbarobo\bin\win64\vccorlib120.dll" "$OUT\package\chbarobo\bin\vccorlib120.dll" 
        Rename "$OUT\package\chbarobo\bin\win64\msvcr120.dll" "$OUT\package\chbarobo\bin\msvcr120.dll" 
    ${Else}
        Rename "$OUT\package\chbarobo\dl\win32\liblinkbot.dl" "$OUT\package\chbarobo\dl\liblinkbot.dl" 
        Rename "$OUT\package\chbarobo\bin\win32\msvcp120.dll" "$OUT\package\chbarobo\bin\msvcp120.dll" 
        Rename "$OUT\package\chbarobo\bin\win32\vccorlib120.dll" "$OUT\package\chbarobo\bin\vccorlib120.dll" 
        Rename "$OUT\package\chbarobo\bin\win32\msvcr120.dll" "$OUT\package\chbarobo\bin\msvcr120.dll" 
    ${Endif}

# Copy chbarobo header files to toolkit/include directory
CopyFiles $OUT\package\chbarobo\include\linkbot.h $OUT\toolkit\include\linkbot.h

SectionEnd

;!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
;!insertmacro MUI_DESCRIPTION_TEXT ${SEC01} $(DESC_python)
;!insertmacro MUI_DESCRIPTION_TEXT ${SEC02} $(DESC_pycharm)
;!insertmacro MUI_FUNCTION_DESCRIPTION_END
